plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.6'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'com.google.cloud.tools.jib' version '3.2.1'
	id 'jacoco'
}

jib {
	from {
		image = "openjdk:11-jre-slim"
		auth {
			username = project.DOCKER_ID
			password = project.DOCKER_PASSWORD
		}
	}
	to {
		image = project.DOCKER_ID + "/" + project.DOCKER_IMAGE_NAME
		auth {
			username = project.DOCKER_ID
			password = project.DOCKER_PASSWORD
		}
		tags = ["latest"]
	}
	container {
		jvmFlags = ["-Xms128m", "-Xmx128m"]
	}
}

group = 'com.bootios'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// spring boot data JPA 의존성
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// spring boot validation 의존성
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// spring boot web 의존성
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// API 문서 자동화 -> spring-doc 방식
	//implementation 'org.springdoc:springdoc-openapi-ui:1.6.8'

	// API 문서 자동화 -> spring-fox 방식
	implementation 'io.springfox:springfox-swagger2:2.6.1'
	implementation 'io.springfox:springfox-swagger-ui:2.6.1'

	// 다국어 제공을 위한 i18n을 yml 파일로 적용하기 위한 의존성
	implementation 'net.rakugakibox.util:yaml-resource-bundle:1.1'

	// view template으로 freemarker 적용
	implementation 'org.springframework.boot:spring-boot-starter-freemarker:2.5.4'

	// spring security 의존성
	implementation 'org.springframework.boot:spring-boot-starter-security'

	// jwt 의존성
	implementation 'io.jsonwebtoken:jjwt:0.9.1'

	// Json을 결과로 매핑하기 위한 의존성
	implementation 'com.google.code.gson:gson:2.8.8'

	compileOnly 'org.projectlombok:lombok'
	// runtimeOnly 'com.h2database:h2'
//	runtimeOnly 'com.mysql:mysql-connector-j'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.8" // 버전 명시
}

jacocoTestReport {
	dependsOn test // 리포트 생성 전에 test를 반드시 수행해야 한다!
	reports { // 어떤 파일들을 생성할지, 어디에 생성할지 설정
		xml.enabled true // xml과 html형식으로 결과물을 만들어내라!
		html.enabled true
		// 경로 명시 안 할 경우 기본 경로는 build/reports/jacoco 이하 경로
	}
}